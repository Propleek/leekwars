include('auto');

//debut de chaque tour après avoir init la map de danger
function updateCoef(idbulb){
	var somme_life_ally = 0;
	var somme_life_enemy= 0;
	var count_ally = 0;
	var count_enemy= 0;
	var targets = _ALIVE_ALLIES+_ALIVE_ENEMIES;
	if(idbulb!=null) targets = [idbulb];
	else{
		_COEF_DMG = [];
		_COEF_HEAL = [];
		_COEF_VACCIN = [];
		_COEF_MAXHP_BOOST = [];
		_COEF_ABSSHIELD = [];
		_COEF_RELSHIELD = [];
		_COEF_STR_BOOST = [];
		_COEF_MP_BOOST = [];
		_COEF_TP_BOOST = [];	
		_COEF_AGI_BOOST = [];
		_COEF_WSD_BOOST = [];
		_COEF_RST_BOOST = [];
		_COEF_SHACKLE_TP = [];
		_COEF_SHACKLE_MP = [];
		_COEF_SHACKLE_STR = [];
		_COEF_SHACKLE_MGC = [];
		_COEF_LIBERATE = [];
		_COEF_RETURN_DMG = [];
	}
	for(var target in targets){
		var infos = _LEEKS[target];
		var eType = getEntityType(target);
		var lifeRatio = getLife(target)/getTotalLife(target);
		if(isAlly(target)){
			if(eType==ENTITY_LEEK_){
				var ignoreleeks=getListOfEnemyLeekWhoPlayAfter(target);
				var danger = getCellDanger(getCell(target), ignoreleeks, _EMPTYARRAY);
				var canDie = target != _SELF && getLife(target) < danger*0.9;
				if(canDie) debugE("canDie: " + getName(target) + ":" + danger);
				somme_life_ally+=infos["life"]; // SUM LIFE ALLY
				count_ally++; // COUNT ALLY LEEK
				_COEF_DMG[target] = -2;
				if(lifeRatio<0.8) _COEF_DMG[target]+= -0.5;
				if(lifeRatio<0.6) _COEF_DMG[target]+= -0.5;
				if(lifeRatio<0.5) _COEF_DMG[target]+= -0.5;
				if(lifeRatio<0.4) _COEF_DMG[target]+= -0.5;
				if(lifeRatio<0.3) _COEF_DMG[target]+= -0.5;
				if(lifeRatio<0.2) _COEF_DMG[target]+= -0.5;
				_COEF_HEAL[target] = -1;
				if(lifeRatio<0.9) _COEF_HEAL[target]+= -1;
				if(lifeRatio<0.7) _COEF_HEAL[target]+= -1;
				if(lifeRatio<0.5) _COEF_HEAL[target]+= -2;
				if(canDie) _COEF_HEAL[target]=-50; // !!!!!!!!!!!!!!!!!!
				if(getLevel(target)<getLevel()/2) _COEF_HEAL[target]/=2;
				// reminder: doit être positif sinon les moins s'annulent !
				_COEF_VACCIN[target] = -0.8;
				if(lifeRatio<0.8) _COEF_VACCIN[target]+= -0.8;
				if(getLevel(target)<getLevel()/2) _COEF_VACCIN[target]/=2;
				_COEF_MAXHP_BOOST[target] = -3.5;
				if(getTotalLife(target)< 2000)_COEF_MAXHP_BOOST[target]+= -1;
				if(getTotalLife(target)< 1600)_COEF_MAXHP_BOOST[target]+= -1;
				if(getTotalLife(target)< 1200)_COEF_MAXHP_BOOST[target]+= -2;
				if(getTotalLife(target)< 800)_COEF_MAXHP_BOOST[target]+= -2;
				_COEF_ABSSHIELD[target] = -1.5;
				if(infos["absShield"]==0) _COEF_ABSSHIELD[target]+= -1;
				if(lifeRatio<0.9)_COEF_ABSSHIELD[target]+= -0.2;
				if(lifeRatio<0.8)_COEF_ABSSHIELD[target]+= -0.2;
				if(lifeRatio<0.7)_COEF_ABSSHIELD[target]+= -0.2;
				if(lifeRatio<0.6)_COEF_ABSSHIELD[target]+= -0.2;
				if(lifeRatio<0.5)_COEF_ABSSHIELD[target]+= -0.2;
				if(lifeRatio<0.4)_COEF_ABSSHIELD[target]+= -0.2;
				if(lifeRatio<0.3)_COEF_ABSSHIELD[target]+= -0.2;
				if(lifeRatio<0.2)_COEF_ABSSHIELD[target]+= -0.2;
				if(canDie) _COEF_ABSSHIELD[target]=-50; // !!!!!!!!!!!!!!!!!!
				_COEF_RELSHIELD[target] = -1.5;
				if(infos["relShield"]==0) _COEF_RELSHIELD[target] += -1;
				if(lifeRatio<0.9)_COEF_RELSHIELD[target]+= -0.2;
				if(lifeRatio<0.8)_COEF_RELSHIELD[target]+= -0.2;
				if(lifeRatio<0.7)_COEF_RELSHIELD[target]+= -0.2;
				if(lifeRatio<0.6)_COEF_RELSHIELD[target]+= -0.2;
				if(lifeRatio<0.5)_COEF_RELSHIELD[target]+= -0.2;
				if(lifeRatio<0.4)_COEF_RELSHIELD[target]+= -0.2;
				if(lifeRatio<0.3)_COEF_RELSHIELD[target]+= -0.2;
				if(lifeRatio<0.2)_COEF_RELSHIELD[target]+= -0.2;
				if(canDie) _COEF_RELSHIELD[target]=-50; // !!!!!!!!!!!!!!!!!!
				_COEF_STR_BOOST[target] = -(getStrength(target)*4/1000);
				if(target!=_SELF && getStrength(target)>0) _COEF_STR_BOOST[target] += -0.1;
				_COEF_MP_BOOST[target] = -2.3;
				if(getMP(target)<9) _COEF_MP_BOOST[target]+= -0.2;
				_COEF_TP_BOOST[target] = -2.3;
				_COEF_AGI_BOOST[target] = -2;
				_COEF_WSD_BOOST[target] = -2;
				_COEF_RST_BOOST[target] = -2;
				_COEF_SHACKLE_TP[target] = -2;
				_COEF_SHACKLE_MP[target] = -2;
				_COEF_SHACKLE_STR[target] = -2;
				_COEF_SHACKLE_MGC[target] = -2;
				_COEF_LIBERATE[target] = -1;
				_COEF_RETURN_DMG[target] = -3;
			}
			else {//bulb
				count_ally+=0.4;
				somme_life_ally+=infos["life"]*0.4; // ???
				_COEF_DMG[target] = -1.5;
				_COEF_HEAL[target] = -1;
				if(lifeRatio<0.8) _COEF_HEAL[target]+= -0.75;
				if(lifeRatio<0.6) _COEF_HEAL[target]+= -0.75;
				if(lifeRatio<0.4) _COEF_HEAL[target]+= -0.75;
				if(lifeRatio==1) _COEF_VACCIN[target]=0;
				else{
					_COEF_VACCIN[target] = -0.75;
					if(lifeRatio<0.9 && lifeRatio>0.5) _COEF_VACCIN[target]+= -0.5;
				}
				if(eType==ENTITY_METALLIC_BULB){
					_COEF_HEAL[target]/=2;
					_COEF_VACCIN[target]/=2;
				}
				_COEF_MAXHP_BOOST[target] = -3;
				if(getTotalLife(target)>800) _COEF_MAXHP_BOOST[target] += 0.5;
				if(getTotalLife(target)>1400) _COEF_MAXHP_BOOST[target] += 0.5;
				if(getTotalLife(target)>2000) _COEF_MAXHP_BOOST[target] += 0.5;
				if(eType==ENTITY_METALLIC_BULB && getTotalLife(target)>1500) _COEF_MAXHP_BOOST[target] += 0.5;
				_COEF_ABSSHIELD[target] = -1.2;
				if(eType==ENTITY_METALLIC_BULB) _COEF_ABSSHIELD[target] = -0.1;
				_COEF_RELSHIELD[target] = -1.2;
				if(eType==ENTITY_METALLIC_BULB) _COEF_RELSHIELD[target] = -0.1;
				_COEF_STR_BOOST[target] = -1.75;
				if(eType==ENTITY_HEALER_BULB) _COEF_STR_BOOST[target] = 0;
				if(eType==ENTITY_METALLIC_BULB) _COEF_STR_BOOST[target] = 0;
				_COEF_MP_BOOST[target] = -2.7 + (getMP(target)/10);
				if(eType==ENTITY_HEALER_BULB) _COEF_MP_BOOST[target]+= 1;
				if(eType==ENTITY_METALLIC_BULB) _COEF_MP_BOOST[target]+= 1.3;
				_COEF_TP_BOOST[target] = -1;
				_COEF_AGI_BOOST[target] = -1;
				_COEF_WSD_BOOST[target] = -1;
				_COEF_RST_BOOST[target] = -1;
				_COEF_SHACKLE_TP[target] = -1;
				_COEF_SHACKLE_MP[target] = -1;
				_COEF_SHACKLE_STR[target] = -1;
				_COEF_SHACKLE_MGC[target] = -1;
				_COEF_LIBERATE[target] = -0.5;
				_COEF_RETURN_DMG[target] = -2;
			}
		}else{//enemy
			_COEF_HEAL[target] = 2;
			_COEF_VACCIN[target] = 2;
			_COEF_MAXHP_BOOST[target] = 2;
			_COEF_ABSSHIELD[target] = 2;
			_COEF_RELSHIELD[target] = 2;
			_COEF_STR_BOOST[target] = 2;
			_COEF_MP_BOOST[target] = 2;
			_COEF_TP_BOOST[target] = 2;
			_COEF_AGI_BOOST[target] = 2;
			_COEF_WSD_BOOST[target] = 2;
			_COEF_RST_BOOST[target] = 2;
			_COEF_RETURN_DMG[target] = 2;
			if(eType==ENTITY_LEEK_){
				somme_life_enemy+=infos["life"]; // SUM LIFE ENEMY
				count_enemy++; // COUNT ENEMY LEEK
				_COEF_DMG[target] = 3;
				if(getTurn()>30) _COEF_DMG[target] += 0.5;
				if(getTurn()>50) _COEF_DMG[target] += 0.5;
				if(lifeRatio<0.6)_COEF_DMG[target]+= 1;
				if(getLevel(target)<getLevel()/2) _COEF_DMG[target] -= 1;
				_COEF_SHACKLE_TP[target] = 5;
				_COEF_SHACKLE_MP[target] = 5;
				_COEF_SHACKLE_STR[target] = 4;
				if(infos["str"]<100) _COEF_SHACKLE_STR[target] = 0.5;
				_COEF_SHACKLE_MGC[target] = 3;
				if(infos["mgc"]<100) _COEF_SHACKLE_MGC[target] = 0.5;
				_COEF_LIBERATE[target] = 1;
			}
			else{//bulb
				somme_life_enemy+=infos["life"]*0.5; // SUM LIFE ENEMY
				count_enemy+=0.5;// COUNT ENEMY BULB
				if(eType==ENTITY_PUNY_BULB) _COEF_DMG[target] = 0.7;
				else if(eType==ENTITY_METALLIC_BULB) _COEF_DMG[target] = 1.8;
				else _COEF_DMG[target] = 2.5;
				_COEF_SHACKLE_TP[target] = 0.25;
				_COEF_SHACKLE_MP[target] = 0.25;
				_COEF_SHACKLE_STR[target] = 1;
				if(eType==ENTITY_HEALER_BULB 
				|| eType==ENTITY_METALLIC_BULB) _COEF_SHACKLE_STR[target] = 0;
				_COEF_SHACKLE_MGC[target] = 0;
				_COEF_LIBERATE[target] = 0.5;
			}
		}
	}// end for(var target in targets)
	if(idbulb==null){
		// fix des vals global
		var ratioPv = somme_life_ally/somme_life_enemy;
		if(ratioPv > 2) _RATIO_IGNORE_DANGER = 0.90;
		else if(ratioPv > 1.75) _RATIO_IGNORE_DANGER = 0.75;
		else if(ratioPv > 1.5) _RATIO_IGNORE_DANGER = 0.60;
		else if(ratioPv > 1.25) _RATIO_IGNORE_DANGER = 0.40;
		else _RATIO_IGNORE_DANGER = 0.20;
		
		
		var ratioCount = count_ally/count_enemy;
		if(ratioCount > 3) _RATIO_DANGER = 0.25;
		else if(ratioCount > 2) _RATIO_DANGER = 0.5;
		else if(ratioCount > 1.5) _RATIO_DANGER = 0.75;
		else _RATIO_DANGER = 1;
		// au cas ou je veux modif ça un jour...
		//_SCORE_KILL_DMG = 200;
		//_SCORE_KILL_PSN = 50;
		//_SCORE_DEATH = 1000;
	}
}
                 
// TODO liberate remove reduc.
function getVirtualState(@leek, @stackOfResult){
    var state = _LEEKS[leek];
	if(stackOfResult[leek]==null) return @state;
	if(stackOfResult[leek]["debuff"]==true){
		state["psnlife"] = state["life"];
		state["psnTurn"] = 0;
		state["psnTotal"] = 0;
		state["absShield"] = 0;
		state["relShield"] = 0;
	}
	if(stackOfResult[leek]["antidote"]==true){
		state["psnlife"] = state["life"];
		state["psnTurn"] = 0;
		state["psnTotal"] = 0;
	}
	state["absShield"]+=stackOfResult[leek]["absShield"];
	state["relShield"]+=stackOfResult[leek]["relShield"];
	state["str"]+=stackOfResult[leek]["str"];
    state["life"]-=stackOfResult[leek]["dmg"]; // dmg
    state["life"]+=stackOfResult[leek]["heal"]; // heal
    if(state["totallife"]<state["life"])state["life"]=state["totallife"];
    state["psnlife"]-=stackOfResult[leek]["psn"];
	state["psnTurn"]+=stackOfResult[leek]["psn"];
    return @state;
}

function getScoreOfAction(@action, @stackOfResult, @stackOfDeath){
	//DEBUG
	startBenchFunction("getScoreOfAction");
	//DEBUG
	var cacheThis = false;
	// gestion du cache
	var uniqId = ""+action+stackOfResult+stackOfDeath;
	if(_CACHE_SCORE_ACTION[uniqId] != null){
		stackOfResult = _CACHE_SCORE_ACTION[uniqId]["stackOfResult"];
		stackOfDeath = _CACHE_SCORE_ACTION[uniqId]["stackOfDeath"];
		stopBenchFunction("getScoreOfAction");
		return _CACHE_SCORE_ACTION[uniqId]["score"];
	}else{
		cacheThis = true;
	}
	
    var me = getVirtualState(_SELF, stackOfResult),
	myStr = me["str"],
    myMgc = me["mgc"],
    myWis = me["wsd"],
	myRst = me["rst"],
	mySnc = me["snc"],
	myAgi = me["agi"],
	myAbsShield = me["absShield"],
	myRelShield = me["relShield"],
    item = action["item"],
    lifeMissing = me["totallife"]-me["life"],
    area = _ITEM_AREA[item],
    isAOE = area==AREA_CIRCLE_1||area==AREA_CIRCLE_2||area==AREA_CIRCLE_3,
    effects = @_ITEM_EFFECTS[item],
    score = 0,
	from = action["from"],
	to = action["to"],
	isSelf = from==to;
	// gestion inaoe
 	if((item == CHIP_DRIP && getCellDistance(from, to)<=2) || item == CHIP_DEVIL_STRIKE){
		if(!inArray(action["targets"], _SELF)) push(action["targets"], _SELF);
		isSelf=true;
	}
	// gestion summon
	if(isSpecialChip(item)){
		if(item==CHIP_RESURRECTION){
			score=_RESU_SCORE;
		}else if(count(_ALIVE_ALLIES_BULBS)>=6) return 0;
		else{
			var cost = _ITEM_COST[item];
			if(item==CHIP_FIRE_BULB){
				cost+=2;//avoid only summoning lightning.
				score -=1;//but prefer lightning over firebulb
			}else if(item==CHIP_HEALER_BULB && lifeMissing>0){
				cost+=1;
			}
			if(_COUNT_FIRE==0 && _COUNT_LIGHTNING==0 && (item==CHIP_FIRE_BULB || item==CHIP_LIGHTNING_BULB)){
				cost+=7; //bonus summon d'un bulb offensif quand y'en a pas.
			}
			score += cost*(140-(count(_ALIVE_ALLIES_BULBS)*4)); // TODO ???
			if(_COUNT_HEALER>=2 && item == CHIP_HEALER_BULB) score/=2;
			if(_COUNT_METAL>=1 && item == CHIP_METALLIC_BULB) score/=2;
		}
	}
	else for(var target in action["targets"]){
        if(target==_SELF && !isSelf) continue; // ignore self, je suis pas sur la case ici !
        if(stackOfDeath[target]!=null) continue; // si la cible est déjà morte, next !
        var virtualTargetState = getVirtualState(target, stackOfResult);
		var targetCell;
		if(target==_SELF) targetCell = from;
		else targetCell = getCell(target);
        var dmgRatio = isAOE? getDamagePercentage(targetCell, to, area) : 100;

        var minDmgValue=0, avgDmgValue=0, turnPsn=0, avgPsn=0, healValue=0, maxhpValue=0, absShieldValue=0, relShieldValue=0, boostStrValue=0, boostAgiValue=0, boostTPValue=0, boostMPValue=0, lifeSteal=0, shackleTPValue=0, shackleMPValue=0, shackleStrValue=0, shackleMgcValue=0, debuffValue=0, antidoteValue=0, returnDmgValue=0;
		var duration;
        for(var e in effects){
			var type = e[0];
			var min = e[1];
			var max = e[2];
			var moy = (min+max)/2;
			var optimistmoy = (max+moy)/2;
			var pessimistmoy = (min+moy)/2;
			duration = e[3];
			duration = duration == 0 ? 1 : // 0 => 1
				(duration == 2 ? 1.75 : // 2 => 1.75
				(duration == 3 ? 2.25 : // 3 => 2.25
				(duration == 4 ? 2.5 : // 4 => 2.5
				duration))); // 1 => 1
            if(type == EFFECT_DAMAGE){
                minDmgValue += ( pessimistmoy*(1+(myStr/100))
                    *(1-(virtualTargetState["relShield"]/100))
                    *(dmgRatio/100)) - virtualTargetState["absShield"];
                avgDmgValue += (optimistmoy*(1+(myStr/100))
                    *(1-(virtualTargetState["relShield"]/100))
                    *(dmgRatio/100)) - virtualTargetState["absShield"];
				if(avgDmgValue<0) avgDmgValue = 0;
            }
            else if(type == EFFECT_POISON){
                turnPsn += pessimistmoy*(1+(myMgc/100))*(dmgRatio/100);
                avgPsn += moy*(1+(myMgc/100))*(dmgRatio/100) *duration;
            }
            else if(type == EFFECT_HEAL){
                healValue = moy*(1+(myWis/100))*(dmgRatio/100)*duration;
            }
			else if(type == EFFECT_BOOST_MAX_LIFE){
				maxhpValue = moy*(1+(myWis/100))*(dmgRatio/100);
			}
			else if(type == EFFECT_ABSOLUTE_SHIELD){
				absShieldValue = moy*(1+(myRst/100))*(dmgRatio/100)*duration;
			}
			else if(type == EFFECT_RELATIVE_SHIELD){
				relShieldValue = moy*(1+(myRst/100))*(dmgRatio/100)*duration;
			}
			else if(type == EFFECT_BUFF_STRENGTH){
				boostStrValue = moy*(1+(mySnc/100))*(dmgRatio/100)*duration;
			}
			else if(type == EFFECT_BUFF_AGILITY
			|| type == EFFECT_BUFF_WISDOM
			|| type == EFFECT_BUFF_RESISTANCE){
				boostAgiValue = moy*(1+(mySnc/100))*(dmgRatio/100)*duration;
				// un seul test pour la simplcité pour le moment.
			}
			else if(type == EFFECT_BUFF_MP){
				boostMPValue = round(moy*(1+(mySnc/100))*(dmgRatio/100))*duration;
			}
			else if(type == EFFECT_BUFF_TP){
				boostTPValue = round(moy*(1+(mySnc/100))*(dmgRatio/100))*duration;
			}
			else if(type == EFFECT_SHACKLE_TP){
				shackleTPValue = round(pessimistmoy*(1+(myMgc-virtualTargetState["snc"])/100)*(dmgRatio/100))*duration;
			}
			else if(type == EFFECT_SHACKLE_MP){
				shackleMPValue = round(pessimistmoy*(1+(myMgc-virtualTargetState["snc"])/100)*(dmgRatio/100))*duration;
			}
			else if(type == EFFECT_SHACKLE_STRENGTH){
				shackleStrValue = round(moy*(1+(myMgc-virtualTargetState["snc"])/100)*(dmgRatio/100))*duration;
			}
			else if(type == EFFECT_SHACKLE_MAGIC){
				shackleMgcValue = round(moy*(1+(myMgc-virtualTargetState["snc"])/100)*(dmgRatio/100))*duration;
			}
			else if(type == EFFECT_DEBUFF){ // LIBERATION !
				debuffValue = _LEEKS[target]["scoreLibe"];
			}
			else if(type == EFFECT_ANTIDOTE){
				antidoteValue= virtualTargetState["psnTotal"];	
			}
			else if(type == EFFECT_KILL){
				if(isSummon(target)&&isAlly(target)){
					minDmgValue = getLife(target);
					avgDmgValue = minDmgValue;
				}
			}
			else if(type == EFFECT_DAMAGE_RETURN){
				returnDmgValue=moy*(1+(myAgi/100))*(dmgRatio/100)*duration;
			}
            else{
				debugE("unhandled:"+getWeaponName(action["item"])+getChipName(action["item"]));
            }
        }
        //kill?
        // ici maybe check if damage pas < à 0 !
		//TODO dans le kill et le normal, faire les revoies de dégâts !
        if(virtualTargetState["life"]<=minDmgValue){
			minDmgValue=virtualTargetState["life"];
            score+=minDmgValue*_COEF_DMG[target];
            stackOfDeath[target]=target;
			var isBulb = isSummon(target) ? 0.75 : 1;
			score+=_SCORE_KILL_DMG*_COEF_DMG[target]*isBulb;
			// mort de ses bulbes
			for(var bulb in virtualTargetState["bulbs"]){
				stackOfDeath[bulb]=bulb;
				score+=_SCORE_KILL_DMG*_COEF_DMG[bulb];
			}
            //lifesteal
            lifeSteal+=minDmgValue*myWis/1000;
            if(lifeSteal>0 && target!=_SELF){
                if(lifeSteal>lifeMissing)lifeSteal=lifeMissing;
                lifeMissing-=lifeSteal;
                score+=lifeSteal*_COEF_HEAL[_SELF];
				if(stackOfResult[_SELF]==null)stackOfResult[_SELF]=[];
                stackOfResult[_SELF]["heal"]+=lifeSteal;
            }
            //lifestealend
        }else if(virtualTargetState["psnlife"]-minDmgValue <= turnPsn){
			// kill poison, pas encore sur de comment je gère ça mieux...
            stackOfDeath[target]=target;
            score+=turnPsn*_COEF_DMG[target];
			score+=_SCORE_KILL_PSN*_COEF_DMG[target];
			// mort de ses bulbes
			for(var bulb in virtualTargetState["bulbs"]){
				stackOfDeath[bulb]=bulb;
				score+=_SCORE_KILL_PSN*_COEF_DMG[bulb];
			}
        }else{
            if(stackOfResult[target]==null)stackOfResult[target]=[];
            if(avgDmgValue){
                stackOfResult[target]["dmg"]+=minDmgValue; // sauvegarde du min
                score +=avgDmgValue*_COEF_DMG[target]; // score de l'avg
                //lifesteal
                lifeSteal+=avgDmgValue*myWis/1000;
                if(lifeSteal>0 && target!=_SELF){
                    if(lifeSteal>lifeMissing)lifeSteal=lifeMissing;
                    lifeMissing-=lifeSteal;
                    score+=lifeSteal*_COEF_HEAL[_SELF];
					if(stackOfResult[_SELF]==null)stackOfResult[_SELF]=[];
                    stackOfResult[_SELF]["heal"]+=lifeSteal;
                }
                //lifestealend
            }
            if(avgPsn){
                stackOfResult[target]["psn"]+=turnPsn;
                var ratioBulb = isSummon(target) ? 0.5 : 1;
                score +=avgPsn*_COEF_DMG[target]*ratioBulb;
            }
            if(healValue){
				if(item==CHIP_VACCINE){
					var onEffect = _LEEKS[target]["effects"][item];
					if(onEffect != null) score+=onEffect*_COEF_VACCIN[target];
					score -=healValue*_COEF_VACCIN[target];
				}else{
					var totallife = virtualTargetState["totallife"];
					var targetMissingLife = totallife-virtualTargetState["life"];
					if(item==CHIP_REGENERATION){
						if(targetMissingLife >= healValue*0.9){
							if(healValue>targetMissingLife) healValue=targetMissingLife;
							stackOfResult[target]["heal"]+=healValue;
							score -=healValue*_COEF_HEAL[target];
							if(isSummon(target))score/=4;
						}else{
							score -=200;
						}
					}else{
						if(healValue>targetMissingLife) healValue=targetMissingLife;
						stackOfResult[target]["heal"]+=healValue;
						score -=healValue*_COEF_HEAL[target];
					}
				}
            }
			if(maxhpValue){
				score -= maxhpValue*_COEF_MAXHP_BOOST[target];
			}
			if(absShieldValue){
				var alreadyHaveArmor = stackOfResult[target]["absShield"] > 0;
				var onEffect = _LEEKS[target]["effects"][item];
				if(onEffect != null) score+=onEffect*_COEF_ABSSHIELD[target];
				else stackOfResult[target]["absShield"]+=absShieldValue/duration;
				score -=absShieldValue*_COEF_ABSSHIELD[target];
				if(alreadyHaveArmor) score/=2;
				if(!_SCN_STR_100) score = 1;// todo del this & handle in coef.
			}
			if(relShieldValue){
				var alreadyHaveArmor = stackOfResult[target]["relShield"] > 0;
				var onEffect = _LEEKS[target]["effects"][item];
				if(onEffect != null) score+=onEffect*4*_COEF_RELSHIELD[target];
				else stackOfResult[target]["relShield"]+=relShieldValue/duration;
				score -=relShieldValue*_SCORE_REL_SHIELD*_COEF_RELSHIELD[target];
				if(alreadyHaveArmor) score/=2;
				//*3 pour que 30% d'armure soit == à 90 armor abs environ
				// je pense que ce c'est discutable, selon les dégâts potentiels de l'adversaire, ptete faire un calcul en début de tour du "potentiel agressif" de la team en face en terme de force/science/magie pour orienter les choix ?
				if(!_SCN_STR_200) score = 1;
			}
			if(boostStrValue){
				var onEffect = _LEEKS[target]["effects"][item];
				if(onEffect != null) score+=onEffect*_COEF_STR_BOOST[target];
				else stackOfResult[target]["str"]+=boostStrValue/duration;
				score -= boostStrValue*_COEF_STR_BOOST[target];
			}
			if(boostAgiValue){
				var onEffect = _LEEKS[target]["effects"][item];
				if(onEffect != null) score+=onEffect*_COEF_AGI_BOOST[target];
				score -= boostAgiValue*_COEF_AGI_BOOST[target];
			}
			if(boostMPValue){
				var onEffect = _LEEKS[target]["effects"][item];
				if(onEffect != null) score+=onEffect*40*_COEF_MP_BOOST[target];
				score -= boostMPValue*_SCORE_MP*_COEF_MP_BOOST[target];//1mp=30str
			}
			if(boostTPValue){
				var onEffect = _LEEKS[target]["effects"][item];
				if(onEffect != null) score+=onEffect*40*_COEF_TP_BOOST[target];
				score -= boostTPValue*_SCORE_TP*_COEF_TP_BOOST[target];//1tp=30str
			}
			if(shackleTPValue){
				score += shackleTPValue*_SCORE_TP*_COEF_SHACKLE_TP[target];
				stackOfResult[target]["shackleTP"]+=shackleMgcValue/duration;
			}
			if(shackleMPValue){
				score += shackleMPValue*_SCORE_MP*_COEF_SHACKLE_MP[target];
				stackOfResult[target]["shackleMP"]+=shackleMPValue/duration;
			}
			if(shackleStrValue){
				score += shackleStrValue*_COEF_SHACKLE_STR[target];
				stackOfResult[target]["shackleStr"]+=shackleStrValue/duration;
			}
			if(shackleMgcValue){
				score += shackleMgcValue*_COEF_SHACKLE_MGC[target];
				stackOfResult[target]["shackleMgc"]+=shackleMgcValue/duration;
			}
			if(debuffValue){
				stackOfResult[target]["debuff"]=true;
				score += debuffValue*_COEF_LIBERATE[target];
			}
			if(antidoteValue){
				stackOfResult[target]["antidote"]=true;
				score -= antidoteValue*_COEF_HEAL[target];
			}
			if(returnDmgValue){
				var onEffect = _LEEKS[target]["effects"][item];
				if(onEffect != null) score+=onEffect*4*_COEF_RETURN_DMG[target];
				else stackOfResult[target]["relShield"]+=returnDmgValue;
				score -=returnDmgValue*_SCORE_REL_SHIELD*_COEF_RETURN_DMG[target];
				if(!_SCN_STR_200) score = 1;
			}
        }
    }
	if(cacheThis){
		_CACHE_SCORE_ACTION[uniqId] = [];
		_CACHE_SCORE_ACTION[uniqId]["stackOfResult"] = stackOfResult;
		_CACHE_SCORE_ACTION[uniqId]["stackOfDeath"] = stackOfDeath;
		_CACHE_SCORE_ACTION[uniqId]["score"] = score;
	}
	stopBenchFunction("getScoreOfAction");
    return @score;
}

function addActionToMAPS(@item, @from, @to, @targets){
	var dist = reachableCells[from];
    if(dist==null)return; // filtering all action I can't reach
	if(assoc_weap_from[item]==null) assoc_weap_from[item] = [];
	push(assoc_weap_from[item], from);
	
	if(assoc_from_item_actions[from]==null) assoc_from_item_actions[from] = [];
	if(assoc_from_item_actions[from][item]==null) assoc_from_item_actions[from][item] = [];
	
	var action = ["item":item, "from":from, "to":to, "targets":targets];
	push(assoc_from_item_actions[from][item], action);
}
function setMapActions(){
	prepareSetMapActions();
	self_action_array = []; // clear
	assoc_weap_from = []; // clear
	assoc_from_item_actions = [];//clear
    var interest;
    for(var w in getWeapons()){
        var weaponArea = _ITEM_AREA[w];    
        if(weaponArea == AREA_POINT){ // normal cible unique.
            for(var target in getAliveEnemies()){ // pas d'ally susceptible d'être une cible.
                var toCell = getCell(target);
                var targets = [target];
                for(var from in _getCellsToUseWeapChipOnCell(w, toCell)){
                    addActionToMAPS(w, from, toCell, targets);
                }
            }
        }else if(weaponArea == AREA_LASER_LINE){ // cas d'un lazer
			var minrange = _ITEM_MINRANGE[w];
	        var maxrange = _ITEM_MAXRANGE[w];			
			for(var leek in _ALIVE_ALLIES+_ALIVE_ENEMIES){
				if(leek==_SELF) continue;
				var toCell = getCell(leek);
				for(var from in getLazerFromCellsToTargetCell(toCell,minrange,maxrange)){
					var targets = getLazerTargetsFromCell(from, toCell, minrange, maxrange);
					if(count(targets)>0){
						interest=0;
						for(var t in targets) interest+=_COEF_DMG[t];
						if(interest>0) addActionToMAPS(w, from, toCell, targets);
						else if(interest<0 && w==WEAPON_B_LASER) addActionToMAPS(w, from, toCell, targets);
					}
				}
			}
        }else{// cas des aoe AREA_CIRCLE_X (1, 2, 3)
        	var cells;
			if(weaponArea==AREA_CIRCLE_1) cells = @atkCellsArea1;
			else if(weaponArea==AREA_CIRCLE_2) cells = @atkCellsArea2;
			else if(weaponArea==AREA_CIRCLE_3) cells = @atkCellsArea3;
            for(var i in cells){
                if(_OBSTACLES[i]==null){
                    var targets = getWeaponTargets(w, i);
                    if(count(targets)>0){
                        interest=0;
                        for(var t in targets)
                            interest+=_COEF_DMG[t]*getDamagePercentage(i, getCell(t), weaponArea)/100;
                        if(interest>0) for(var from in _getCellsToUseWeapChipOnCell(w, i)){
                            addActionToMAPS(w, from, i, targets);
                        }
                    }
                }
            }
        }
    }// end for weapons
    for(var c in getChips()){
        if(getCooldown(c)!=0) continue;
        // pour le moment on gère pas les special chip
        var chipArea = _ITEM_AREA[c];
        // cannot be lazer_line
        if(isSpecialChip(c)){
			// TODO handle resu, inversion & teleport..
			self_action_array[c] = c;
		}else if(chipArea==AREA_POINT){
            var targets;
			if(c==CHIP_LIBERATION){
				targets = _ALIVE_ALLIES+_ALIVE_ENEMIES;
			}else if(_CHIP_ISPOSITIV[c]){
				if(_CHIP_ISONLYBULB[c]) targets = _ALIVE_ALLIES_BULBS;
				else targets = _ALIVE_ALLIES;
			}else{
				targets = _ALIVE_ENEMIES;
			}
			for(var target in targets){
				// self_actions !
                if(target==_SELF && _ITEM_MINRANGE[c]==0 && self_action_array[c]==null){
					self_action_array[c] = c;
				}else if(target!=_SELF){	// others.
					var toCell = getCell(target);
					for(var from in _getCellsToUseWeapChipOnCell(c, toCell)){
						addActionToMAPS(c, from, toCell, [target]);
					}
				}
            }
        }else if(chipArea==AREA_LASER_LINE){
            debugE("CHIP LAZERLINE ?!?");
        }else{// cas des aoe AREA_CIRCLE_X (1, 2, 3)
        // TODO maybe opti en faisant qu'une fois les calcs par area pour weapon & chip
            var isPositiv = isPositivChip(c);
			var cells;
			if(isPositiv){
				if(chipArea==AREA_CIRCLE_1) cells = @defCellsArea1;
				else if(chipArea==AREA_CIRCLE_2) cells = @defCellsArea2;
				else if(chipArea==AREA_CIRCLE_3) cells = @defCellsArea3;
			}else{
				if(chipArea==AREA_CIRCLE_1) cells = @atkCellsArea1;
				else if(chipArea==AREA_CIRCLE_2) cells = @atkCellsArea2;
				else if(chipArea==AREA_CIRCLE_3) cells = @atkCellsArea3;
			}
            for(var i in cells){
                if(_OBSTACLES[i]==null){
                    var targets = getChipTargets(c, i);
                    if(count(targets)>0){
						interest=0;
                        for(var t in targets){
						// FIXME DMG ???? est-ce que je prend pas en fonction de l'effet de la puce ? mais ça va couté un bras ?
                            if(isPositiv)interest -= _COEF_DMG[t]* getDamagePercentage(i, getCell(t), chipArea)/100;
                            else interest += _COEF_DMG[t]* getDamagePercentage(i, getCell(t), chipArea)/100;
                        }
                        if(interest>0) for(var from in _getCellsToUseWeapChipOnCell(c, i)){
                            addActionToMAPS(c, from, i, targets);
                        }
                    }
                }
            }
        }
    }//end for chips
}

function findBestMove(@allComboz, @gravityArray){
	// filtre les combos dont un item n'a pas le CD
	var allCombos = [];
	for(var name:var combo in allComboz){
		var ok = true;
		for(var item in combo){
			if(_ITEM_HAVECD[item] && getCooldown(item)!=0){
				ok=false;
				break;
			}
		}
		if(ok) push(allCombos, combo);
	}

	//raz du cache.
	_CACHE_SCORE_SAFECELL = [];
	_CACHE_SCORE_ACTION = [];
	var _OP_LIMIT = OPERATIONS_LIMIT*0.98;
	if(_IS_SUMMONER) _OP_LIMIT-= _OPERATION_BULB*(_LEEKS[_SELF]["nbBulbs"]+1);
	var _BESTMOVE=null;
	var selfCell = getCell();
	var selfMP = getMP();
	var selfLife = getLife();
	var lifePercent = selfLife/getTotalLife();
	var ratioRisk = 100 + getLevel()*2;
	var cntCombo = count(allCombos);
	
	//var __DEBUG_STRINGINFO = "";
	
	for(var combo in allCombos){
		var canAlterDanger = false;
		for(var item in combo) if(_ITEM_CAN_ALTER_DANGER[item]) {canAlterDanger=true; break;}
		cntCombo--;
		var fromArray = [];
		// find cells where every part of combo can be done.
		if(count(combo)==0){ // special case empty combo for safe play.
			push(fromArray, getCell());
		}else{	
			for(var c:var d in reachableCells){
				if(_OBSTACLES[c]!=null) continue;
				var ok = true, isOnlySelf = true;
				for(var item in combo){
					if(assoc_from_item_actions[c][item]==null){
						if(self_action_array[item]==null){
							ok = false;
							break;
						}
					}else{
						isOnlySelf = false;
					}
				}
				if(ok){
					if(isOnlySelf){ if(c==selfCell) { push(fromArray, c); }
					}else{ push(fromArray, c); }
				}
			}
		}
		// DEBUG
		//for(var item in combo) __DEBUG_STRINGINFO += "-" + _ITEMID_TOSTRING[item];
		//__DEBUG_STRINGINFO += ": " + count(fromArray) + "\n";
		// END DEBUG
		for(var aCell in fromArray){
			var mpMove1 = reachableCells[aCell];
			var mpLeft = selfMP - mpMove1;
			if(mpMove1 != null && mpLeft >= 0){
				var score = 0, tmpScore = 0, best = [], total;
				var stack = [], dead = [];
				// calcul du score de la meilleure combo depuis cette case.
				for(var item in combo){
					// meilleure score pour chaque item
					var actions = @assoc_from_item_actions[aCell][item];
					var aScore = 0, aBest, aStack, aDead;
					if(actions!=null){
						for(var action in actions){
							var tmpstack=stack, tmpdead=dead;
							tmpScore=getScoreOfAction(action, tmpstack, tmpdead);
							if(aBest == null || aScore < tmpScore){
								aScore = tmpScore;
								aBest = action;
								aStack = tmpstack;
								aDead = tmpdead;
							}
						}
					}//si actions est null, alors self_action ne peut pas être null.
					if(self_action_array[item]!=null){
						var tmpstack=stack, tmpdead=dead;
						//creation d'une action self sur cette case.
						var action = ["item":item, 
						"from":aCell, "to":aCell,
						"targets":[_SELF]];
						tmpScore=getScoreOfAction(action, tmpstack, tmpdead);
						if(aBest == null || aScore < tmpScore){
							aScore = tmpScore;
							aBest = action;
							aStack = tmpstack;
							aDead = tmpdead;
						}
					}
					if(aBest != null && aScore>0){
						aBest["score"] = aScore; // pour le debug
						push(best, aBest);
						stack = aStack;
						dead = aDead;
						score += aScore;
					}else{
						//l'action n'a pas été push car inutile, les TP restant sont libre pour lancer un buff
						//mayeb faire autrement, je laisse le commentaire ici.
					}
				}
				// à partir du score de la meilleure combo depuis aCell, recherche de la meilleure case safe.
				startBenchFunction("getBestSafeCellScore");
				var uniqId = canAlterDanger ? getUniqueID_safeCell(aCell, dead, stack) : getUniqueID_safeCell(aCell, dead, "");
				if(_CACHE_SCORE_SAFECELL[uniqId]==null){
					var ignored = [selfCell];
					for(var d in dead) push(ignored, getCell(d));
					
					if(_CACHE_REACHABLE_DANGER[aCell]==null){
						_CACHE_REACHABLE_DANGER[aCell] = getReachableCells([aCell:0], mpLeft, ignored);
						_CACHE_REACHABLE_SIZE[aCell] = mpLeft;
					}else if(_CACHE_REACHABLE_SIZE[aCell] < mpLeft){
						_CACHE_REACHABLE_DANGER[aCell] = getReachableCells(_CACHE_REACHABLE_DANGER[aCell], mpLeft-_CACHE_REACHABLE_SIZE[aCell], ignored);
						_CACHE_REACHABLE_SIZE[aCell] = mpLeft;
					}
					var reachable = @_CACHE_REACHABLE_DANGER[aCell];
					
					
					var result = getBestSafeCellScore(reachable, dead, stack, selfLife, lifePercent, gravityArray, mpLeft);
					_CACHE_SCORE_SAFECELL[uniqId] = result;
				}
				var bestSafeMove = @_CACHE_SCORE_SAFECELL[uniqId];
				stopBenchFunction("getBestSafeCellScore");
				var safestCell = bestSafeMove["sCell"];
				var safeScore = bestSafeMove["score"];
				var minDanger = bestSafeMove["danger"];
				
				var finalScore = score + safeScore; // cumul des actions

				if(_BESTMOVE==null || _BESTMOVE["score"]<finalScore) _BESTMOVE = [
					"score":finalScore,
					"actions":best,
					//"aCell":aCell,
					"sCell":safestCell,
					"danger":minDanger,
					"stack":stack,//pour le debuging
					"dead":dead//pour le debuging
				];
				if(getOperations()>_OP_LIMIT){
					debugE("Turn"+getTurn()+" breaked ! Combos left: "+ cntCombo +"/"+count(allCombos));
					// DEBUG
					//debug(__DEBUG_STRINGINFO);
					// END DEBUG
					return @_BESTMOVE;
				}
			}
		}
	}
	// DEBUG
	//debug(__DEBUG_STRINGINFO);
	// END DEBUG
	return @_BESTMOVE;
}

function getBestSafeCellScore(@reachable, @dead, @stack, @selfLife, @lifePercent, @gravityArray, @mpLeft){
	var minDanger = 9999999;
	var bestScore = -9999999;
	var safestCell;
	for(var sCell:var mpused in reachable){
		if(mpused > mpLeft) break;
		var danger = getCellDanger(sCell, dead, stack);
		var tmpScore = 0;
		if(danger > selfLife*_RATIO_IGNORE_DANGER) tmpScore-= danger*_RATIO_DANGER;
		if(lifePercent<0.6) tmpScore*=2;
		if(danger >= selfLife*0.95) tmpScore-=_SCORE_DEATH;
		for(var cell in _areaCells1[sCell]) if(_LEEKS_CELL[cell]!=null) tmpScore-=150;
		for(var cell in _areaCells2[sCell]) if(_LEEKS_CELL[cell]!=null) tmpScore-=20;
		for(var cell:var dist in gravityArray){
			tmpScore -= abs(dist-getCellDistance(cell, sCell));
		}
		if(safestCell==null || tmpScore > bestScore){
			safestCell = sCell;
			minDanger = danger;
			bestScore = tmpScore;
		}
	}
	return @["sCell":safestCell, "danger":minDanger, "score":bestScore];
}

function playBestMove(bestMove){
	debug('score: ' + bestMove["score"]);
	debug('danger:' + bestMove["danger"]);
	debug('dead: ' + bestMove["dead"]);
	var isBulb = isSummon(getLeek());
	var delayedSummon = [];
	for(var action in bestMove["actions"]){
		var item = action["item"], from = action["from"], to = action["to"];
		mark(from, COLOR_BLUE);
		moveTowardCell(from);
		if(isSpecialChip(item)){ // GESTION SUMMON
			push(delayedSummon, item);
		}else if(_ITEM_ISWEAP[item]){
			if(getWeapon()!=item) setWeapon(item);
			useWeaponOnCell(to);
		}
		else useChipOnCell(item, to);
		debug(_ITEMID_TOSTRING[item]+": "+action["score"]);
		
		// on compte les utilisations :
		if(getLevel()>=32 && !isBulb){
			var reg = getRegister(_ITEMID_TOSTRING[item]);
			if(reg==null)setRegister(_ITEMID_TOSTRING[item],1);
			else setRegister(_ITEMID_TOSTRING[item],number(reg)+1);
		}
	}
	if(bestMove["sCell"]!=null){
		mark(bestMove["sCell"], COLOR_GREEN);
		moveTowardCell(bestMove["sCell"]);
	}
	for(var item in delayedSummon){
		if(item == CHIP_RESURRECTION){
			resurrect(_RESU_TARGET, getCellToUseChip(item, _SELF));
		}else{
			summon(item, getCellToUseChip(item, _SELF), iaBulb);
		}
	}
}

/////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////
//====================================== BULB =====================================//
/////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////

function findBestMoveBulb(@allCombos, @maxOp){
	// raz du cache
	_CACHE_SCORE_ACTION = [];
	var stopingOp = getOperations()+maxOp;
	var _BESTMOVE=null;
	var self = getLeek();
	var selfCell = getCell();
	var selfMP = getMP();
	var selfLife = getLife();
	var cntCombo = count(allCombos);
	for(var combo in allCombos){
		cntCombo--;
		var fromArray = [];
		// find cells where every part of combo can be done.
		for(var c:var d in reachableCells){
			if(_OBSTACLES[c]!=null) continue;
			var ok = true, isOnlySelf = true;
			for(var item in combo){
				if(assoc_from_item_actions[c][item]==null){
					if(self_action_array[item]==null){
						ok = false;
						break;
					}
				}else{
					isOnlySelf = false;
				}
			}
			if(ok){
				if(isOnlySelf){ if(c==selfCell) { push(fromArray, c); }
				}else{ push(fromArray, c); }
			}
		}
		for(var aCell in fromArray){
			var mpMove1 = reachableCells[aCell];
			var mpLeft = selfMP - mpMove1;
			if(mpMove1 != null && mpLeft >= 0){
				var score = 0, tmpScore = 0, best = [], total;
				var stack = [], dead = [];
				// calcul du score de la meilleure combo depuis cette case.
				for(var item in combo){
					// meilleure score pour chaque item
					var actions = @assoc_from_item_actions[aCell][item];
					var aScore = 0, aBest, aStack, aDead;
					if(actions!=null){
						for(var action in actions){
							var tmpstack=stack, tmpdead=dead;
							tmpScore=getScoreOfAction(action, tmpstack, tmpdead);
							if(aBest == null || aScore < tmpScore){
								aScore = tmpScore;
								aBest = action;
								aStack = tmpstack;
								aDead = tmpdead;
							}
						}
					}//si actions est null, alors self_action ne peut pas être null.
					if(self_action_array[item]!=null){
						var tmpstack=stack, tmpdead=dead;
						//creation d'une action self sur cette case.
						var action = ["item":item, 
						"from":aCell, "to":aCell,
						"targets":[_SELF]];
						tmpScore=getScoreOfAction(action, tmpstack, tmpdead);
						if(aBest == null || aScore < tmpScore){
							aScore = tmpScore;
							aBest = action;
							aStack = tmpstack;
							aDead = tmpdead;
						}
					}
					if(aBest != null && aScore>0){
						aBest["score"] = aScore; // pour le debug
						push(best, aBest);
						stack = aStack;
						dead = aDead;
						score += aScore;
					}else{
						// useless...
					}
				}
				
				if(_BESTMOVE==null || _BESTMOVE["score"]<score) _BESTMOVE = [
					"score":score,
					"actions":best,
					"stack":stack,//pour le debuging
					"dead":dead//pour le debuging
				];
				if(getOperations()>stopingOp){
					debugE("Turn"+getTurn()+" breaked bulb ! Combos left: "+ cntCombo +"/"+count(allCombos));
					return @_BESTMOVE;
				}
			}
		}
	}
	return @_BESTMOVE;
}

// environ 26k pour 7pm
function getSafestCell(@dead, @gravityArray, @ratio_ignore){
	var minDanger = 9999999;
	var bestScore = -9999999;
	var selfLife = getLife();
	var reachable = getReachableCells([getCell():0], getMP(), []);
	var safestCell;
	for(var sCell:var mpused in reachable){
		var danger = getCellDanger(sCell, dead, []);
		if(count(_ALIVE_ALLIES_BULBS)>4) danger = 0;
		var tmpScore = 0;
		if(danger > selfLife*ratio_ignore) tmpScore-= danger*_RATIO_DANGER;
		if(danger >= selfLife) tmpScore-=_SCORE_DEATH;
		for(var cell in _areaCells1[sCell]) if(_LEEKS_CELL[cell]!=null) tmpScore-=250;
		for(var cell in _areaCells2[sCell]) if(_LEEKS_CELL[cell]!=null) tmpScore-=100;
		for(var cell:var dist in gravityArray){
			tmpScore -= abs(dist-getCellDistance(cell, sCell));
		}
		if(safestCell==null || tmpScore > bestScore){
			safestCell = sCell;
			minDanger = danger;
			bestScore = tmpScore;
		}
	}
	return @safestCell;//@["sCell":safestCell, "danger":minDanger, "score":bestScore];
}

function iaBulb(){
	var limitOp = _OPERATION_BULB*0.7;
	refreshLeeksCellsAndAliveStatus();
	refreshLeeksEffectsAndStates();
	if(getTurn()==getBirthTurn()) updateCoef(getLeek());
	var ratio_ignore = 0.25+_RATIO_IGNORE_DANGER;// ratio de pv que j'ignore pour m'avancer sans mourir
	if(count(_ALIVE_ALLIES_BULBS)>4) ratio_ignore+=0.25;
	var gravityArray = [getCell(getNearestEnemy()):4, getCell(getSummoner()):4];
	if(getEntityType(getLeek())==ENTITY_HEALER_BULB){
		ratio_ignore=0;
		gravityArray = [getCell(getFarthestAlly()):4, getCell(getSummoner()):4];
	}
	var combos = getComboSet(getLeek());
	var finalCombos = []; // filter cooldown
	for(var combo in combos){
		var ok = true;
		for(var item in combo){
			if(_ITEM_HAVECD[item] && getCooldown(item)!=0){
				ok=false;
				break;
			}
		}
		if(ok) push(finalCombos, combo);
	}
	reachableCells = getReachableCells([getCell():0], getMP(), []);
	startOp();
	setMapActions();
	stopOpkW("BULB mapAction");
	startOp();
	var bestMove = findBestMoveBulb(finalCombos, limitOp);
	stopOpkW("BULB findBest");
	debugW("BEST: "+bestMove);
	if(bestMove!=null) playBestMove(bestMove);
	startOp();
	var dead = bestMove==null ? [] : bestMove["dead"]; 
	var selectedCell = getSafestCell(dead, gravityArray, ratio_ignore);
	moveTowardCell(selectedCell);
	stopOpkW("BULB safeCell");
	debugW('TOTALOP: '+(getOperations()/1000)+'k');
}
